plugins {
    id 'idea'
    id 'java-library'
    id "com.vanniktech.maven.publish" version "0.36.0"
}

repositories {
    maven { url = uri("https://maven.aliyun.com/repository/public") }
    mavenCentral()
}

java {
    javadoc {
        options.encoding = "UTF-8"
    }
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    // Hutool Core
    implementation 'cn.hutool:hutool-core:5.8.43'

    // Hutool JSON
    implementation 'cn.hutool:hutool-json:5.8.43'

    // Apache FreeMarker
    implementation 'org.freemarker:freemarker:2.3.34'

    // CommonMark Java Core
    implementation 'org.commonmark:commonmark:0.27.1'

    // CommonMark Java Extension For Tables
    implementation 'org.commonmark:commonmark-ext-gfm-tables:0.25.1'

    // HTML Compressor and Minifier
    implementation 'com.googlecode.htmlcompressor:htmlcompressor:1.5.2'

    // YUI Compressor
    implementation 'com.yahoo.platform.yui:yuicompressor:2.4.8'

    // Jsoup Java HTML Parser
    implementation 'org.jsoup:jsoup:1.22.1'

    // Project Lombok
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
    testCompileOnly 'org.projectlombok:lombok:1.18.42'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'

    // TestNG
    testImplementation 'org.testng:testng:7.5.1'
}

// 单元测试
test {
    useTestNG {
        includeGroups 'apiResolver', 'generator'
    }
}

// 版本
version = "2.0.3"

// 生成胖jar包
tasks.withType(Jar) {
    description = '生成胖jar包'
    archiveVersion = project.version
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from sourceSets.main.output
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}

import com.vanniktech.maven.publish.JavaLibrary
import com.vanniktech.maven.publish.JavadocJar

// 发布
// 本地发布：gradlew publishToMavenLocal
// Maven中央库发布：gradlew publishToMavenCentral
mavenPublishing {
    coordinates("io.github.dfengwei", "api-doc", version)
    configure(new JavaLibrary(new JavadocJar.Javadoc(), true))
    pom {
        name = "api-doc"
        description = "基于java注释的接口文档生成器"
        inceptionYear = "2025"
        url = "https://github.com/dfengwei/api-doc"
        licenses {
            license {
                name = 'MIT License'
                url = 'https://opensource.org/license/mit'
            }
        }
        developers {
            developer {
                id = 'dfengwei'
                email = 'dfengwei@163.com'
            }
        }
        scm {
            connection = 'scm:git:git://github.com/dfengwei/api-doc.git'
            developerConnection = 'scm:git:ssh://github.com/dfengwei/api-doc.git'
            url = 'https://github.com/dfengwei/api-doc.git'
        }
    }
}

// 编译时，使用UTF-8编码
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}
